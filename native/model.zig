const std = @import("std");
const mx = @import("mlx.zig");
const lanes = @import("lanes.zig");
const src = @import("kernel_sources.zig");
const A = mx.Array;
const ti = mx.ti;
const Weights = @import("weights.zig").Weights;
const kv = @import("kv_buffer.zig");
pub const Cache = struct {
    a: A = mx.empty,
    b: A = mx.empty,
    keys: kv.Buffer = .{},
    values: kv.Buffer = .{},
    // Forward records borrow these from their Pass scope; persistent clones omit them.
    key_write: kv.Write = .{},
    value_write: kv.Write = .{},
    pub fn clone(c: Cache) !Cache {
        var out = Cache{};
        errdefer out.deinit();
        if (c.a.ctx != null) out.a = try mx.retain(c.a);
        if (c.b.ctx != null) out.b = try mx.retain(c.b);
        out.keys = try c.keys.clone();
        out.values = try c.values.clone();
        return out;
    }
    pub fn deinit(c: *Cache) void {
        mx.free(c.a);
        mx.free(c.b);
        c.keys.deinit();
        c.values.deinit();
        c.* = .{};
    }
};
pub const Record = struct {
    values: [8]A = @splat(mx.empty),
    // Shared rounds keep the original conv state separate from values[6]'s window rows.
    conv_state: A = mx.empty,
    key_write: kv.Write = .{},
    value_write: kv.Write = .{},
};
pub const Pass = struct {
    scope: mx.Scope = .{},
    logits: A = mx.empty,
    hidden: A = mx.empty,
    records: [64]Record = @splat(.{}),
    taps: [5]A = @splat(mx.empty),
    parents: [2048]i32 = undefined,
    count: usize = 0,
    start: i32 = 0,
    vision_delta: ?i32 = null,
    pub fn deinit(p: *Pass) void {
        p.scope.deinit();
    }
};
pub const CompiledPost = struct {
    closure: mx.c.mlx_closure = .{ .ctx = null },
    payload: ?*Payload = null,
    const arrays = .{ "weight", "sb", "scales", "biases", "signs" };
    const Payload = struct {
        scope: mx.Scope = .{},
        kernels: mx.Kernels,
        norm: A,
        linears: [3]lanes.Linear,
        stack: ?lanes.Linear,
        failure: ?anyerror = null,

        fn destroy(raw: ?*anyopaque) callconv(.c) void {
            const p: *Payload = @ptrCast(@alignCast(raw.?));
            p.kernels.deinit();
            p.scope.deinit();
            mx.allocator.destroy(p);
        }
        fn callback(out: [*c]mx.c.mlx_vector_array, ins: mx.c.mlx_vector_array, raw: ?*anyopaque) callconv(.c) c_int {
            const p: *Payload = @ptrCast(@alignCast(raw.?));
            return p.graph(out, ins) catch |err| {
                p.failure = err;
                return -1;
            };
        }
        fn graph(p: *Payload, out: [*c]mx.c.mlx_vector_array, ins: mx.c.mlx_vector_array) !c_int {
            if (mx.c.mlx_vector_array_size(ins) != 2) return error.InvalidKernelArity;
            var s = mx.Scope{};
            defer s.deinit();
            var args: [2]A = undefined;
            for (&args, 0..) |*arg, j| {
                var value = mx.c.mlx_array_new();
                const rc = mx.c.mlx_vector_array_get(&value, ins, j);
                arg.* = try s.result(rc, value);
            }
            const post = try lanes.norm(&p.kernels, &s, args[0], args[1], p.norm);
            const rows = mx.dim(post.x.x, 1);
            const act = if ((rows < 17 or rows > 32) and p.stack != null)
                try lanes.mlpStack(&p.kernels, &s, try p.stack.?.apply(&p.kernels, &s, post.x))
            else
                try lanes.mlp(&p.kernels, &s, try p.linears[0].apply(&p.kernels, &s, post.x), try p.linears[1].apply(&p.kernels, &s, post.x));
            const values = [_]A{ post.h, try p.linears[2].apply(&p.kernels, &s, act) };
            return mx.c.mlx_vector_array_set_data(out, &values, values.len);
        }
    };

    pub fn init(norm: A, linears: [3]lanes.Linear, stack: ?lanes.Linear) !CompiledPost {
        var value = Payload{ .kernels = mx.Kernels.init(), .norm = norm, .linears = linears, .stack = stack };
        var transferred = false;
        errdefer if (!transferred) {
            value.kernels.deinit();
            value.scope.deinit();
        };
        value.norm = try value.scope.own(try mx.retain(norm));
        for (&value.linears) |*linear| try retainLinear(&value.scope, linear);
        if (value.stack) |*linear| try retainLinear(&value.scope, linear);
        // Evaluated constants share storage across all row specializations.
        try mx.evalMany(value.scope.arrays.items, false);
        const payload = try mx.allocator.create(Payload);
        payload.* = value;
        transferred = true;
        const fun = mx.c.mlx_closure_new_func_payload(Payload.callback, payload, Payload.destroy);
        if (fun.ctx == null) {
            Payload.destroy(payload);
            return error.MlxFailure;
        }
        defer _ = mx.c.mlx_closure_free(fun);
        var closure = mx.c.mlx_closure{ .ctx = null };
        errdefer if (closure.ctx != null) {
            _ = mx.c.mlx_closure_free(closure);
        };
        try mx.check(mx.c.mlx_compile(&closure, fun, false));
        return .{ .closure = closure, .payload = payload };
    }
    fn retainLinear(s: *mx.Scope, linear: *lanes.Linear) !void {
        if (linear.signs.ctx != null) return error.InvalidCompiledProjection;
        inline for (arrays) |field| if (@field(linear, field).ctx != null) {
            @field(linear, field) = try s.own(try mx.retain(@field(linear, field)));
        };
    }
    pub fn deinit(p: *CompiledPost) void {
        if (p.closure.ctx != null) _ = mx.c.mlx_closure_free(p.closure);
        p.* = .{};
    }
    pub fn call(p: *CompiledPost, kernels: *mx.Kernels, s: *mx.Scope, h: A, r: A) ![2]A {
        p.payload.?.failure = null;
        var result: [2]A = undefined;
        kernels.call(s, p.closure, &.{ h, r }, &result) catch |err| return p.payload.?.failure orelse err;
        return result;
    }
};
pub const Model = struct {
    pub const SerialPass = Pass;
    round_owner: @import("decode_round.zig").Owner = .{},
    weights: Weights,
    kernels: mx.Kernels,
    projection_cache: ?*lanes.ProjectionCache = null,
    posts: [64]CompiledPost = @splat(.{}),
    cache: [64]Cache = @splat(.{}),
    position: i32 = 0,
    rope_delta: i32 = 0,
    prefill_ops: @import("prefill_ops.zig").Ops = .{},
    trace_dir: ?[]const u8 = null,
    pub fn init(io: std.Io, dir: []const u8) !Model {
        var m = Model{ .weights = Weights.init(), .kernels = mx.Kernels.init() };
        errdefer m.deinit();
        try m.weights.load(io, dir);
        return m;
    }
    pub fn reset(m: *Model) void {
        for (&m.cache) |*c| c.deinit();
        m.position = 0;
        m.rope_delta = 0;
    }
    pub fn deinit(m: *Model) void {
        m.reset();
        for (&m.posts) |*post| post.deinit();
        m.weights.deinit();
        m.kernels.deinit();
        m.prefill_ops.deinit();
    }
    pub fn weight(m: *Model, index: usize, suffix: []const u8) !A {
        var buf: [192]u8 = undefined;
        return m.weights.get(try std.fmt.bufPrint(&buf, "model.layers.{d}.{s}", .{ index, suffix }));
    }
    pub fn project(m: *Model, s: *mx.Scope, index: usize, suffix: []const u8, x: lanes.Act) !A {
        var buf: [192]u8 = undefined;
        var l = try m.weights.linear(try std.fmt.bufPrint(&buf, "model.layers.{d}.{s}", .{ index, suffix }));
        if (m.projection_cache) |cache| if (l.signs.ctx != null) {
            const input = try cache.prepare(l, &m.kernels, s, x);
            if (mx.tensor_units and l.rotation_id != 0) inline for (.{
                .{ "self_attn.k_proj", "self_attn.v_proj" },
                .{ "mlp.gate_proj", "mlp.up_proj" },
            }) |group| {
                var selected: ?usize = null;
                inline for (group, 0..) |name, j| if (std.mem.eql(u8, suffix, name)) {
                    selected = j;
                };
                if (selected) |part| if (!std.mem.eql(u8, group[0], "mlp.gate_proj") or mx.dim(x.x, 1) < 17 or mx.dim(x.x, 1) > 32) {
                    var buffers: [group.len][192]u8 = undefined;
                    var names: [group.len][]const u8 = undefined;
                    inline for (group, 0..) |name, j| names[j] = try std.fmt.bufPrint(&buffers[j], "model.layers.{d}.{s}", .{ index, name });
                    if (try m.weights.fused(&names)) |stack| {
                        var offset: i32 = 0;
                        for (names[0..part]) |name| offset += (try m.weights.linear(name)).n;
                        return s.slice(try cache.project(stack, &m.kernels, s, input), 2, offset, offset + l.n);
                    }
                };
            };
            l.signs = mx.empty;
            return l.apply(&m.kernels, s, input);
        };
        // Pre-M5 plain projections retain the stacked SIMD reduction.
        if (!mx.tensor_units and l.format != null and l.signs.ctx == null) inline for (.{
            .{ "linear_attn.in_proj_qkv", "linear_attn.in_proj_z", "linear_attn.in_proj_b", "linear_attn.in_proj_a" },
            .{ "self_attn.q_proj", "self_attn.k_proj", "self_attn.v_proj" },
            .{ "mlp.gate_proj", "mlp.up_proj" },
        }) |group| {
            var member = false;
            inline for (group) |name| if (std.mem.eql(u8, suffix, name)) {
                member = true;
            };
            if (member) {
                var compatible = true;
                var width: i32 = 0;
                var members: [group.len]lanes.Linear = undefined;
                inline for (group, 0..) |name, j| {
                    const other = try m.weights.linear(try std.fmt.bufPrint(&buf, "model.layers.{d}.{s}", .{ index, name }));
                    members[j] = other;
                    if (other.signs.ctx != null or !std.meta.eql(l.format, other.format) or l.k != other.k or other.scales.ctx == null or mx.dtype(l.scales) != mx.dtype(other.scales)) compatible = false;
                    width += other.n;
                }
                if (compatible) {
                    const reduction: i32 = if (width <= 64) 32 else if (width <= 6144) 16 else 8;
                    if (l.simdBitsFits()) return l.simdBitsRows(&m.kernels, s, x.x, reduction, try lanes.Linear.prepareSimdGroup(&m.kernels, &members));
                    return l.applyWithReduction(&m.kernels, s, x, reduction);
                }
            }
        };
        return l.apply(&m.kernels, s, x);
    }
    pub fn mlpAct(m: *Model, s: *mx.Scope, index: usize, x: lanes.Act) !lanes.Act {
        const rows = mx.dim(x.x, 1);
        if (rows < 17 or rows > 32) if (try m.projectStack(s, index, &.{ "mlp.gate_proj", "mlp.up_proj" }, x)) |gate_up|
            return lanes.mlpStack(&m.kernels, s, gate_up);
        return lanes.mlp(&m.kernels, s, try m.project(s, index, "mlp.gate_proj", x), try m.project(s, index, "mlp.up_proj", x));
    }
    pub fn postAttention(m: *Model, s: *mx.Scope, index: usize, h: A, r: A) ![2]A {
        if (mx.tensor_units and m.weights.bonsai_form == null) {
            const compiled = &m.posts[index];
            if (compiled.closure.ctx == null) {
                var buffers: [3][192]u8 = undefined;
                var names: [3][]const u8 = undefined;
                inline for (.{ "mlp.gate_proj", "mlp.up_proj", "mlp.down_proj" }, 0..) |suffix, j|
                    names[j] = try std.fmt.bufPrint(&buffers[j], "model.layers.{d}.{s}", .{ index, suffix });
                const stack = try m.weights.fused(names[0..2]);
                var linears: [3]lanes.Linear = undefined;
                for (&linears, names) |*linear, name| linear.* = try m.weights.linear(name);
                compiled.* = try CompiledPost.init(try m.weight(index, "post_attention_layernorm.weight"), linears, stack);
            }
            return compiled.call(&m.kernels, s, h, r);
        }
        const normalized = try lanes.norm(&m.kernels, s, h, r, try m.weight(index, "post_attention_layernorm.weight"));
        return .{ normalized.h, try m.project(s, index, "mlp.down_proj", try m.mlpAct(s, index, normalized.x)) };
    }
    pub fn projectStack(m: *Model, s: *mx.Scope, index: usize, comptime suffixes: []const []const u8, x: lanes.Act) !?A {
        if (mx.tensor_units) if (m.projection_cache) |cache| {
            var buffers: [suffixes.len][192]u8 = undefined;
            var names: [suffixes.len][]const u8 = undefined;
            inline for (suffixes, 0..) |suffix, i| names[i] = try std.fmt.bufPrint(&buffers[i], "model.layers.{d}.{s}", .{ index, suffix });
            const first = try m.weights.linear(names[0]);
            if (try m.weights.fused(&names)) |stack| {
                const input = try cache.prepare(first, &m.kernels, s, x);
                return try cache.project(stack, &m.kernels, s, input);
            }
        };
        return null;
    }
    pub fn prefillProject(m: *Model, s: *mx.Scope, index: usize, suffix: []const u8, x: A) !A {
        var buf: [192]u8 = undefined;
        const l = try m.weights.linear(try std.fmt.bufPrint(&buf, "model.layers.{d}.{s}", .{ index, suffix }));
        return l.prefill(&m.kernels, s, x);
    }
    pub fn prefill(m: *Model, tokens: []const i32) !Pass {
        return @import("qwen_prefill.zig").forward(m, tokens);
    }
    pub fn prefillImage(m: *Model, tokens: []const i32, embeddings: A, positions: A, delta: i32) !Pass {
        return @import("qwen_prefill.zig").forwardImage(m, tokens, embeddings, positions, delta);
    }
    pub fn trace(m: *Model, s: *mx.Scope, position: i32, layer: usize, label: []const u8, value: A) !void {
        const dir = m.trace_dir orelse return;
        var buf: [4096]u8 = undefined;
        const path = try std.fmt.bufPrintSentinel(&buf, "{s}/{d}-{d}-{s}.npy", .{ dir, position, layer, label }, 0);
        const out = try s.cast(value, mx.f32t);
        try mx.eval(out);
        try mx.check(mx.c.mlx_save(path, out));
    }
    pub fn forward(m: *Model, tokens: []const i32, parents: []const i32) !Pass {
        if (tokens.len != parents.len) return error.InvalidTree;
        var s = mx.Scope{};
        defer s.deinit();
        var p = try m.forwardTokens(try s.ints(tokens), parents);
        errdefer p.deinit();
        try mx.eval(p.logits);
        try observeBuffers(&p);
        return p;
    }
    pub fn forwardStreams(m: *Model, streams: []const @import("qwen_shared.zig").Stream) !@import("qwen_shared.zig").Pass {
        return @import("qwen_shared.zig").forward(m, streams);
    }
    pub fn observeBuffers(p: *Pass) !void {
        if (!kv.track_reuse) return;
        for (p.records) |rec| {
            try kv.observe(rec.key_write);
            try kv.observe(rec.value_write);
        }
    }
    pub fn forwardSerialArray(m: *Model, tokens: A) !Pass {
        if (tokens.ctx == null or mx.c.mlx_array_size(tokens) != 1) return error.InvalidToken;
        return m.forwardTokens(tokens, &.{-1});
    }
    fn forwardTokens(m: *Model, tokens: A, parents: []const i32) !Pass {
        if (mx.c.mlx_array_ndim(tokens) != 1 or mx.c.mlx_array_size(tokens) != parents.len) return error.InvalidTree;
        if (mx.dtype(tokens) != mx.i32t and mx.dtype(tokens) != mx.c.MLX_UINT32) return error.InvalidToken;
        const tree = try lanes.Tree.init(parents);
        var p = Pass{ .count = parents.len, .start = m.position };
        @memcpy(p.parents[0..parents.len], parents);
        errdefer p.deinit();
        const s = &p.scope;
        var projection_cache = lanes.ProjectionCache{};
        const previous_cache = m.projection_cache;
        m.projection_cache = &projection_cache;
        defer m.projection_cache = previous_cache;
        const kernels = &m.kernels;
        var h = try m.weights.embedArray(s, tokens);
        var pending: ?A = null;
        var positions: [128]i32 = undefined;
        for (0..parents.len) |i| positions[i] = m.position + m.rope_delta + tree.depths[i];
        const pos = try s.ints(positions[0..parents.len]);
        for (0..64) |i| {
            const inorm = try lanes.norm(kernels, s, h, pending, try m.weight(i, "input_layernorm.weight"));
            h = inorm.h;
            const r = if (i % 4 == 3) try m.attn(s, i, inorm.x, &tree, pos, &p.records[i]) else try m.gdn(s, i, inorm.x, &tree, &p.records[i]);
            const post = try lanes.norm(kernels, s, h, r, try m.weight(i, "post_attention_layernorm.weight"));
            h = post.h;
            const act = try m.mlpAct(s, i, post.x);
            pending = try m.project(s, i, "mlp.down_proj", act);
            // DFlash taps are the post-residual layer outputs, before the next norm.
            for ([_]usize{ 5, 19, 33, 47, 61 }, 0..) |layer, j| if (i == layer) {
                p.taps[j] = try s.binary(mx.c.mlx_add, h, pending.?);
            };
            if (i == 0 or (i + 1) % 4 == 0) try mx.evalMany(&.{ h, pending.? }, true);
        }
        const normed = try lanes.norm(kernels, s, h, pending, try m.weights.get("model.norm.weight"));
        p.hidden = normed.x.x;
        try m.trace(s, m.position, 64, "decode-hidden", normed.x.x);
        p.logits = try (try m.weights.linear("lm_head")).apply(kernels, s, normed.x);
        try m.trace(s, m.position, 64, "decode-logits", p.logits);
        return p;
    }
    fn attn(m: *Model, s: *mx.Scope, i: usize, x: lanes.Act, t: *const lanes.Tree, pos: A, rec: *Record) !A {
        const w: i32 = @intCast(t.parents.len);
        const qg = try s.reshape(try m.project(s, i, "self_attn.q_proj", x), &.{ 1, w, 24, 512 });
        var q = try s.rms(try s.slice(qg, 3, 0, 256), try m.weight(i, "self_attn.q_norm.weight"));
        const gate = try s.reshape(try s.slice(qg, 3, 256, 512), &.{ 1, w, 6144 });
        var key = try s.rms(try s.reshape(try m.project(s, i, "self_attn.k_proj", x), &.{ 1, w, 4, 256 }), try m.weight(i, "self_attn.k_norm.weight"));
        var value = try s.transpose(try s.reshape(try m.project(s, i, "self_attn.v_proj", x), &.{ 1, w, 4, 256 }), &.{ 0, 2, 1, 3 });
        q = try s.transpose(try s.rope(try s.transpose(q, &.{ 1, 2, 0, 3 }), pos, 64), &.{ 2, 1, 0, 3 });
        key = try s.transpose(try s.rope(try s.transpose(key, &.{ 1, 2, 0, 3 }), pos, 64), &.{ 2, 1, 0, 3 });
        rec.values[0] = key;
        rec.values[1] = value;
        if (kv.enabled) {
            rec.key_write = try m.cache[i].keys.append(s, m.cache[i].a, key, 2);
            rec.value_write = try m.cache[i].values.append(s, m.cache[i].b, value, 2);
            key = rec.key_write.capacity;
            value = rec.value_write.capacity;
        } else if (m.cache[i].a.ctx != null) {
            key = try s.cat(&.{ m.cache[i].a, key }, 2);
            value = try s.cat(&.{ m.cache[i].b, value }, 2);
        }
        const out = try s.reshape(try s.transpose(try lanes.attentionCapacity(&m.kernels, s, q, key, value, t, m.position + w), &.{ 0, 2, 1, 3 }), &.{ 1, w, 6144 });
        return m.project(s, i, "self_attn.o_proj", .{ .x = try s.binary(mx.c.mlx_multiply, out, try s.unary(mx.c.mlx_sigmoid, gate)) });
    }
    fn gdn(m: *Model, s: *mx.Scope, i: usize, x: lanes.Act, t: *const lanes.Tree, rec: *Record) !A {
        const w: i32 = @intCast(t.parents.len);
        const mp = @divTrunc(w + 15, 16) * 16;
        const qkv = try m.project(s, i, "linear_attn.in_proj_qkv", x);
        const z = try m.project(s, i, "linear_attn.in_proj_z", x);
        const b = try m.project(s, i, "linear_attn.in_proj_b", x);
        const a = try m.project(s, i, "linear_attn.in_proj_a", x);
        const cs = if (m.cache[i].a.ctx != null) m.cache[i].a else try s.zeros(&.{ 1, 3, 10240 }, mx.bf16);
        const st = if (m.cache[i].b.ctx != null) m.cache[i].b else try s.zeros(&.{ 1, 48, 128, 128 }, mx.f32t);
        const cw = try s.reshape(try m.weight(i, "linear_attn.conv1d.weight"), &.{ 10240, 4 });
        const vals = try m.kernels.run(s, src.lane_glue_gdn_pre, &.{ qkv, cs, cw, try s.ints(t.windows[0 .. t.parents.len * 4]), a, b, try m.weight(i, "linear_attn.A_log"), try m.weight(i, "linear_attn.dt_bias") }, &.{ ti("NK", 16), ti("NV", 48), ti("DK", 128), ti("DV", 128), ti("TAPS", 4) }, .{ 32, 80, w }, .{ 32, 1, 1 }, &.{ .{ .shape = &.{ 1, w, 16, 128 } }, .{ .shape = &.{ 1, w, 16, 128 } }, .{ .shape = &.{ 1, w, 48, 128 } }, .{ .shape = &.{ 1, w, 48 }, .dtype = mx.f32t }, .{ .shape = &.{ 1, w, 48 } } });
        const y = (try m.kernels.run(s, src.lane_tree_tree, &.{ vals[0], vals[1], vals[2], vals[3], vals[4], st, try s.ints(t.parents), try s.ints(&.{w}) }, &.{ mx.td("InT", mx.bf16), ti("Dk", 128), ti("Dv", 128), ti("Hk", 16), ti("Hv", 48), ti("MAXW", if (t.chain) 1 else if (w <= 16) 16 else 32), mx.tb("CHAIN", t.chain) }, .{ 32, 128, 48 }, .{ 32, 4, 1 }, &.{.{ .shape = &.{ 1, w, 48, 128 } }}))[0];
        @memcpy(rec.values[0..5], vals[0..5]);
        // A pass must own its replay base even if the caller restores/replaces the
        // live cache before committing a different accepted path from this pass.
        rec.values[5] = try s.own(try mx.retain(st));
        rec.values[6] = try s.cat(&.{ cs, qkv }, 1);
        const post = try m.kernels.run(s, src.lane_glue_gdn_post, &.{ y, z, try m.weight(i, "linear_attn.norm.weight"), try s.scalar(1e-6), try s.ints(&.{ w, mp }) }, &.{ ti("NV", 48), ti("DV", 128) }, .{ 32, 48, mp }, .{ 32, 1, 1 }, &.{ .{ .shape = &.{ 1, w, 6144 } }, .{ .shape = &.{ 96, mp }, .dtype = mx.f32t } });
        return m.project(s, i, "linear_attn.out_proj", .{ .x = post[0], .sums = post[1] });
    }
    pub fn commit(m: *Model, p: *Pass, rows: []const i32) !void {
        return m.commitImpl(p, rows, true);
    }
    pub fn commitSerialQueued(m: *Model, p: *Pass) !void {
        if (p.count != 1) return error.InvalidCommit;
        return m.commitImpl(p, &.{0}, false);
    }
    fn commitImpl(m: *Model, p: *Pass, rows: []const i32, evaluate: bool) !void {
        if (rows.len == 0) return error.EmptyCommit;
        if (m.position != p.start or rows.len > p.count or rows[0] != 0) return error.InvalidCommit;
        if (p.vision_delta != null and rows.len != p.count) return error.PartialImagePrefillCommit;
        for (rows, 0..) |row, i| {
            if (row < 0 or row >= p.count or p.parents[@intCast(row)] != (if (i == 0) @as(i32, -1) else rows[i - 1])) return error.InvalidCommit;
        }
        var commit_scope = mx.Scope{};
        defer commit_scope.deinit();
        const s = &commit_scope;
        const ids = try s.ints(rows);
        const count = try s.ints(&.{@intCast(rows.len)});
        // Construct all replacement handles before publishing the new cache.
        // Serial pipelining evaluates the graphs with the next draw; regular
        // commits also wait for GPU execution before publishing.
        var next: [64]Cache = @splat(.{});
        errdefer for (&next) |*c| c.deinit();
        for (&p.records, 0..) |*rec, i| {
            const v = rec.values;
            if (i % 4 == 3) {
                var consecutive = true;
                for (rows, 0..) |row, j| if (row != j) {
                    consecutive = false;
                };
                if (kv.enabled and consecutive and rec.key_write.capacity.ctx != null) {
                    const end = m.position + @as(i32, @intCast(rows.len));
                    next[i].a = try mx.retain(try s.slice(rec.key_write.capacity, 2, 0, end));
                    next[i].b = try mx.retain(try s.slice(rec.value_write.capacity, 2, 0, end));
                    next[i].keys = try m.cache[i].keys.finish(s, rec.key_write, @intCast(rows.len));
                    next[i].values = try m.cache[i].values.finish(s, rec.value_write, @intCast(rows.len));
                    continue;
                }
                var keys = try s.take(v[0], ids, 2);
                var vals = try s.take(v[1], ids, 2);
                if (kv.enabled) {
                    // A branched path needs compaction. Verification reused a spare;
                    // commit writes the gathered path into a protected replacement.
                    const kw = try m.cache[i].keys.append(s, m.cache[i].a, keys, 2);
                    const vw = try m.cache[i].values.append(s, m.cache[i].b, vals, 2);
                    keys = kw.view;
                    vals = vw.view;
                    next[i].keys = try m.cache[i].keys.finish(s, kw, @intCast(rows.len));
                    next[i].values = try m.cache[i].values.finish(s, vw, @intCast(rows.len));
                } else if (m.cache[i].a.ctx != null) {
                    keys = try s.cat(&.{ m.cache[i].a, keys }, 2);
                    vals = try s.cat(&.{ m.cache[i].b, vals }, 2);
                }
                next[i].a = try mx.retain(keys);
                next[i].b = try mx.retain(vals);
            } else {
                const state = if (v[7].ctx != null and rows.len == p.count) v[7] else (try m.kernels.run(s, src.lane_tree_replay, &.{ v[0], v[1], v[2], v[3], v[4], v[5], ids, count }, &.{ ti("Dk", 128), ti("Dv", 128), ti("Hk", 16), ti("Hv", 48), mx.td("StT", mx.f32t) }, .{ 32, 128, 48 }, .{ 32, 4, 1 }, &.{.{ .shape = &.{ 1, 48, 128, 128 }, .dtype = mx.f32t }}))[0];
                var tail: [3]i32 = undefined;
                for (0..3) |j| {
                    const n = @as(i32, @intCast(rows.len)) + @as(i32, @intCast(j));
                    tail[j] = if (n < 3) n else 3 + rows[@intCast(n - 3)];
                }
                const sequence = if (rec.conv_state.ctx != null) try s.cat(&.{ rec.conv_state, v[6] }, 1) else v[6];
                next[i].a = try mx.retain(try s.contiguous(try s.take(sequence, try s.ints(&tail), 1)));
                next[i].b = try mx.retain(state);
            }
        }
        var arrays: [128]A = undefined;
        for (next, 0..) |c, i| {
            arrays[2 * i] = c.a;
            arrays[2 * i + 1] = c.b;
        }
        if (evaluate) try mx.evalMany(&arrays, false);
        for (&m.cache) |*c| c.deinit();
        m.cache = next;
        m.position += @intCast(rows.len);
        if (p.vision_delta) |delta| m.rope_delta = delta;
    }
};

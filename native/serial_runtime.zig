const std = @import("std");
const mx = @import("mlx.zig");
const sampling = @import("sampling.zig");
const Stopwatch = @import("vendor/io_util.zig").Stopwatch;

pub fn run(comptime M: type, init: std.process.Init, args: []const []const u8) !void {
    const a = init.gpa;
    const io = init.io;
    var prompt: []const u8 = "Write a short Python function that computes the Fibonacci sequence.";
    var token_list: ?[]const u8 = null;
    var max_tokens: usize = 32;
    var drafts: usize = if (@hasDecl(M, "propose")) 3 else 0;
    var settings = sampling.Sampling{};
    var seed_set = false;
    var report: ?[]const u8 = null;
    var dump: ?[]const u8 = null;
    var drafter: ?[]const u8 = null;
    var drafter_bits: i32 = 8;
    var exact = false;
    var long_cache = false;
    var batched_prefill = true;
    var warm = false;
    var ignore_eos = false;
    var i: usize = 3;
    while (i < args.len) : (i += 1) {
        const key = args[i];
        if (std.mem.eql(u8, key, "--ignore-eos")) {
            ignore_eos = true;
            continue;
        }
        if (std.mem.eql(u8, key, "--warmup")) {
            warm = true;
            continue;
        }
        if (std.mem.eql(u8, key, "--lane-prefill")) {
            batched_prefill = false;
            continue;
        }
        if (std.mem.eql(u8, key, "--no-drafts")) {
            drafts = 0;
            continue;
        }
        if (std.mem.eql(u8, key, "--metal-simd")) {
            mx.force_simd = true;
            continue;
        }
        if (std.mem.eql(u8, key, "--metal-sampling")) {
            settings.metal = true;
            continue;
        }
        if (std.mem.eql(u8, key, "--check-exact")) {
            exact = true;
            continue;
        }
        if (std.mem.eql(u8, key, "--check-long-cache")) {
            long_cache = true;
            continue;
        }
        if (i + 1 >= args.len) return error.MissingArgument;
        const value = args[i + 1];
        if (std.mem.eql(u8, key, "--drafter-bits")) {
            drafter_bits = try std.fmt.parseInt(i32, value, 10);
            i += 1;
            continue;
        }
        if (std.mem.eql(u8, key, "--drafter")) {
            drafter = value;
            i += 1;
            continue;
        }
        if (std.mem.eql(u8, key, "--mtp-drafts")) {
            drafts = try std.fmt.parseInt(usize, value, 10);
            i += 1;
            continue;
        }
        if (std.mem.eql(u8, key, "--prompt")) prompt = value else if (std.mem.eql(u8, key, "--tokens")) token_list = value else if (std.mem.eql(u8, key, "--max-tokens")) max_tokens = try std.fmt.parseInt(usize, value, 10) else if (std.mem.eql(u8, key, "--temperature")) settings.temperature = try std.fmt.parseFloat(f64, value) else if (std.mem.eql(u8, key, "--top-k")) settings.top_k = try std.fmt.parseInt(usize, value, 10) else if (std.mem.eql(u8, key, "--top-p")) settings.top_p = try std.fmt.parseFloat(f64, value) else if (std.mem.eql(u8, key, "--min-p")) settings.min_p = try std.fmt.parseFloat(f64, value) else if (std.mem.eql(u8, key, "--seed")) {
            settings.seed = try std.fmt.parseInt(u64, value, 10);
            seed_set = true;
        } else if (std.mem.eql(u8, key, "--report")) report = value else if (std.mem.eql(u8, key, "--dump-logits")) dump = value else return error.UnsupportedArgument;
        i += 1;
    }
    try settings.validate();
    if (drafts > 15) return error.InvalidDraftBudget;
    if (@hasDecl(M, "validateDraft")) {
        if (drafts == 0) drafter = null else if (drafter) |dir| try M.validateDraft(io, dir);
    }
    if (@hasDecl(M, "prepareRuntime")) try M.prepareRuntime();
    try mx.init();
    defer mx.shutdown();
    const load_timer = Stopwatch.init(io);
    var model = try M.init(io, args[2]);
    defer model.deinit();
    if (drafter) |dir| {
        if (@hasDecl(M, "loadDraftBits")) try model.loadDraftBits(io, dir, drafter_bits) else if (@hasDecl(M, "loadDraft")) try model.loadDraft(io, dir) else return error.UnsupportedDrafts;
    }
    if (@hasField(M, "has_mtp")) if (!model.has_mtp) {
        drafts = 0;
    };
    try mx.check(mx.c.mlx_synchronize(mx.stream));
    const load_seconds = @as(f64, @floatFromInt(load_timer.read())) / 1e9;
    if (exact or long_cache) {
        try model.checkExact(33);
        if (long_cache) for ([_]usize{ 1022, 1150, 2302 }) |prefix| try model.checkExact(prefix);
        return;
    }
    const path = try std.Io.Dir.cwd().realPathFileAlloc(io, args[2], a);
    defer a.free(path);
    var tokenizer = try @import("vendor/tokenizer.zig").loadTokenizer(io, a, path);
    defer tokenizer.deinit();
    var tokens: std.ArrayList(i32) = .empty;
    defer tokens.deinit(a);
    if (token_list) |list| {
        var split = std.mem.splitScalar(u8, list, ',');
        while (split.next()) |value| try tokens.append(a, try std.fmt.parseInt(i32, value, 10));
    } else {
        const ids = try tokenizer.encode(a, prompt);
        defer a.free(ids);
        for (ids) |id| try tokens.append(a, @intCast(id));
    }
    if (tokens.items.len == 0) return error.EmptyPrompt;
    if (tokens.items.len > 262144 or max_tokens > 262144 - tokens.items.len) return error.ContextLimitExceeded;
    const vocab = if (@hasField(M, "vocab")) model.vocab else M.vocab;
    for (tokens.items) |id| if (id < 0 or id >= vocab) return error.InvalidToken;
    if (!seed_set) settings.seed = sampling.seedFor(tokens.items);
    const warm_timer = Stopwatch.init(io);
    if (warm) {
        var warmed = try @import("serial_generation.zig").generateWithPrefill(io, &model, tokens.items, max_tokens, settings, drafts, null, batched_prefill, ignore_eos);
        warmed.deinit();
        model.reset();
        try mx.check(mx.c.mlx_synchronize(mx.stream));
    }
    const warmup_seconds = if (warm) @as(f64, @floatFromInt(warm_timer.read())) / 1e9 else 0;
    var generated = try @import("serial_generation.zig").generateWithPrefill(io, &model, tokens.items, max_tokens, settings, drafts, dump, batched_prefill, ignore_eos);
    defer generated.deinit();
    const text = try tokenizer.decode(a, generated.tokens.items, false);
    defer a.free(text);
    var buffer: [4096]u8 = undefined;
    var out = std.Io.File.stdout().writer(io, &buffer);
    try out.interface.writeAll(text);
    try out.interface.writeAll("\n");
    try out.interface.flush();
    if (report) |file| {
        var digest: [32]u8 = undefined;
        std.crypto.hash.sha2.Sha256.hash(std.mem.sliceAsBytes(generated.tokens.items), &digest, .{});
        const bytes = try std.json.Stringify.valueAlloc(a, .{ .prompt_tokens = tokens.items, .tokens = generated.tokens.items, .text = text, .seed = settings.seed, .temperature = settings.temperature, .top_k = settings.top_k, .top_p = settings.top_p, .min_p = settings.min_p, .metal_sampling = settings.metal, .rounds = generated.rounds, .drafted = generated.drafted, .accepted = generated.accepted, .load_seconds = load_seconds, .warmup_seconds = warmup_seconds, .prefill_seconds = generated.prefill_seconds, .decode_seconds = generated.decode_seconds, .token_sha256 = std.fmt.bytesToHex(digest, .lower) }, .{});
        defer a.free(bytes);
        const f = try std.Io.Dir.cwd().createFile(io, file, .{});
        defer f.close(io);
        try f.writeStreamingAll(io, bytes);
    }
}
